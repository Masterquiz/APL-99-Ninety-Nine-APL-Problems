:Namespace prolog_lists
    
    ⎕IO ⎕ML ← 1

    'cmat' ⎕CY 'dfns' ⍝  Dyalog's functions: https://dfns.dyalog.com/n_contents.htm

    last ← ⊃∘⌽
    but_last ← 2⊃⌽
    element_at ← ⊃
    length ← ≢
    reverse ← ⌽
    is_palindrom ← ⊢≡⌽
    flatten ← ∊
    compress ← ⊢⊢⍤/⍨1,2≠/⊢
    pack ← ⊢⊂⍨1,2≠/⊢
    encode ← {(≢,⊃)¨⍵⊂⍨1,2≠/⍵}
    encode_mod ← {(1~⍨≢,⊃)¨⍵⊂⍨1,2≠/⍵}
    decode_mod ← {∊(//)¨⊆⍵}
    encode_dir ← ⊢ ⍝ Not solved
    duplicate ← 2∘/
    replicate ← /
    drop_every ← {⍵/⍨0≠⍺|⍳≢⍵}
    slice ← ↑,⍥⊂↓
    split ← {⍵/⍨1=(⍺+0 1)⍸⍳≢⍵}
    rotate ← ⌽
    remove_at ← {⍵[(⍳≢⍵)~⍺]}
    insert_at ← {pos char←⍺ ⋄ char@pos⊢⍵\⍨pos≠⍳1+≢⍵}
    range ← {⍺,⍺+⍳⍵-⍺}
    rnd_select ← {⍵[⍺?≢⍵]}
    lotto ← ?
    rnd_select ← ⊂∘(?⍨≢)⌷⊢
    comb ← cmat
    group ← ⊢ ⍝ Not solved
    lsort ← {⍵[⍋≢¨⍵]}
    lfsort ← {⍵⌷⍨{,/⍵[⍋≢¨⍵]}⊢∘↓⌸≢¨⍵}

:EndNamespace
